%{
/* File: src/command/keyword.gperf (or the .c file generated from it by gperf)
 * Part of cards <github.com/rmkrupp/cards>
 *
 * Copyright (C) 2024 Noah Santer <n.ed.santer@gmail.com>
 * Copyright (C) 2024 Rebecca Krupp <beka.krupp@gmail.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
#include "command/keyword.h"
%}

%compare-strncmp
%readonly-tables
%enum
%define slot-name offset
%define lookup-function-name keyword_lookup
%define string-pool-name keyword_pool
%includes
%omit-struct-type
%struct-type
%pic

struct keyword_lookup_result;

%%
SAY, KEYWORD_SAY
EXIT, KEYWORD_EXIT
SHUTDOWN, KEYWORD_SHUTDOWN
LIFE, KEYWORD_LIFE
ENERGY, KEYWORD_ENERGY
SOURCES, KEYWORD_SOURCES
MY, KEYWORD_MY
PLAYER, KEYWORD_PLAYER
HAND, KEYWORD_HAND
DECK, KEYWORD_DECK
DISCARD, KEYWORD_DISCARD
GRAVE, KEYWORD_GRAVE
ZONE, KEYWORD_ZONE
ID, KEYWORD_ID
SPECIAL, KEYWORD_SPECIAL
POSITION, KEYWORD_POSITION
CARD, KEYWORD_CARD
IN, KEYWORD_IN
LOOK, KEYWORD_LOOK
%%

/* offset->string function because we are using %pic */
const char * __attribute__ ((const)) keyword_string(int offset)
{
    return &keyword_pool[offset];
}

